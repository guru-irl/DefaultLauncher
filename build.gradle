import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    dependencies {
        classpath GRADLE_CLASS_PATH
        classpath PROTOBUF_CLASS_PATH
        classpath KOTLIN
    }
}

plugins {
    id "com.google.devtools.ksp" version "2.3.3"
}

final String ANDROID_TOP = "${rootDir}"
final String FRAMEWORK_PREBUILTS_DIR = "${ANDROID_TOP}/prebuilts/libs/"

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion COMPILE_SDK

    namespace = 'com.android.launcher3'

    defaultConfig {
        applicationId 'com.guru.defaultlauncher'
        minSdkVersion 33
        targetSdkVersion 36
        versionCode project.hasProperty('versionCode') ? project.property('versionCode').toInteger() : 1
        versionName project.hasProperty('versionName') ? project.property('versionName') : "1.0-dev"

        buildConfigField "boolean", "IS_STUDIO_BUILD", "false"
        buildConfigField "boolean", "QSB_ON_FIRST_SCREEN", "false"
        buildConfigField "boolean", "IS_DEBUG_DEVICE", "false"
        buildConfigField "boolean", "WIDGET_ON_FIRST_SCREEN", "true"
        buildConfigField "boolean", "WIDGETS_ENABLED", "true"
        buildConfigField "boolean", "NOTIFICATION_DOTS_ENABLED", "true"

        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            def keystorePath = project.hasProperty('KEYSTORE_FILE') ? project.property('KEYSTORE_FILE') : null
            if (keystorePath != null) {
                storeFile file(keystorePath)
                storePassword project.property('KEYSTORE_PASSWORD')
                keyAlias project.property('KEY_ALIAS')
                keyPassword project.property('KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            def keystorePath = project.hasProperty('KEYSTORE_FILE') ? project.property('KEYSTORE_FILE') : null
            if (keystorePath != null) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src', 'shared/src']
            assets.srcDirs = ['assets']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDirs = ['protos/']
            }
        }
    }
    kotlinOptions {
        jvmTarget = '21'
    }

    buildFeatures {
        buildConfig = true
    }

}

// 启用增量注解处理（提升编译速度）
kapt {
    useBuildCache = true
    correctErrorTypes = true
    javacOptions {
//        option("-Xmaxerrs", 1000)
    }
}

allprojects {
    ext {
        addFrameworkJar = { String name ->
            def frameworkJar = new File(FRAMEWORK_PREBUILTS_DIR, name)
            if (!frameworkJar.exists()) {
                throw new IllegalArgumentException("Framework jar path ${frameworkJar.path} doesn't exist")
            }
            gradle.projectsEvaluated {
                tasks.withType(JavaCompile).configureEach {
                    classpath = files(frameworkJar, classpath)
                }
                tasks.withType(KotlinCompile).configureEach {
                    libraries.from(files(frameworkJar))
                }
            }
            dependencies {
                compileOnly files(frameworkJar)
            }
        }

        compileOnlyCommonJars = {
            dependencies {
                compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'SystemUI-core.jar')
                compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'SystemUI-statsd.jar')
            }
        }
    }
}

// Apply framework-16.jar to all subprojects so hidden APIs are available
subprojects {
    afterEvaluate {
        def frameworkJar = new File(FRAMEWORK_PREBUILTS_DIR, 'framework-16.jar')
        if (frameworkJar.exists()) {
            gradle.projectsEvaluated {
                tasks.withType(JavaCompile).configureEach {
                    classpath = files(frameworkJar, classpath)
                }
                tasks.withType(KotlinCompile).configureEach {
                    libraries.from(files(frameworkJar))
                }
            }
            dependencies {
                compileOnly files(frameworkJar)
            }
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.dynamicanimation
    implementation libs.androidx.preference
    implementation libs.androidx.slice.builders
    implementation libs.androidx.graphics.shapes
    implementation libs.androidx.window
    implementation libs.androidx.core.animation
    implementation libs.androidx.material3
    implementation libs.material
    implementation libs.kotlinx.coroutines

    implementation libs.protobuf.javalite

    // mxparser for calculator (same as Kvaesitso)
    implementation 'org.mariuszgromada.math:MathParser.org-mXparser:6.1.0'

    api libs.lottie
    implementation libs.dagger
    kapt libs.dagger.compiler

}

dependencies {
    implementation project(':IconLoader')
    implementation project(':Animation')
    implementation project(':Shared')
    implementation project(':msdl')
    implementation project(':flags')
    implementation project(':WMShared')
}

dependencies {
    implementation files('prebuilts/libs/SystemUI-statsd.jar')
    implementation files('prebuilts/libs/am_flags_lib.jar')
    implementation files('prebuilts/libs/com_android_window_flags.jar')
    implementation files('prebuilts/libs/com_android_systemui_flags.jar')
    implementation files('prebuilts/libs/com_android_systemui_shared_flags.jar')
    implementation files('prebuilts/libs/com_android_wm_shell_flags.jar')
    implementation files('prebuilts/libs/PlatformAnimationLib.jar')
    implementation files('prebuilts/libs/PlatformAnimationLib_kotlin.jar')
    implementation files('prebuilts/libs/PluginAnnotationLib.jar')
    implementation files('prebuilts/libs/PluginAnnotationLib_kotlin.jar')
    implementation files('prebuilts/libs/PluginCoreLib.jar')
    implementation files('prebuilts/libs/PluginCoreLib_kotlin.jar')
    implementation files('prebuilts/libs/view_capture_proto.jar')
    implementation files('prebuilts/libs/tracinglib-platform.jar')
    implementation files('prebuilts/libs/view_capture.jar')
    implementation files('prebuilts/libs/view_capture_kotlin.jar')
    compileOnly files('prebuilts/libs/framework-16.jar')
}

def protocVersion = '4.33.1'//'3.8.0'

protobuf {
    // Configure the protoc executable
    protoc {
        if (osdetector.os == "osx") {
            artifact = "com.google.protobuf:protoc:${protocVersion}:osx-x86_64"
        } else {
            //        artifact = "com.google.protobuf:protoc:${protocVersion}${PROTO_ARCH_SUFFIX}"
            artifact = "com.google.protobuf:protoc:${protocVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
                java {
                    option "lite"
                }
            }
        }
    }
}
